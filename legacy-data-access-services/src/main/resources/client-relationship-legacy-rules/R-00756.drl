import gov.ca.cwds.rest.exception.IssueDetails
import gov.ca.cwds.data.legacy.cms.entity.Client
import gov.ca.cwds.data.legacy.cms.entity.ClientRelationship
import gov.ca.cwds.cms.data.access.dto.ClientRelationshipAwareDTO;
import java.time.LocalDate;

global java.util.Set<IssueDetails> validationDetailsList

rule "R-00756"
  dialect "mvel"
  agenda-group "client-relationship-agenda"
  when
      /*
        Code: R - 00756

        Rule Txt:
          The start date cannot be less than the birth date (if it exists) of either the
          Primary or Secondary Client. This rule only applies when both the primary and secondary
          Clients have birth dates entered.

      */
      $relationshipEntityAwareDto: ClientRelationshipAwareDTO()
      $relationship: ClientRelationship() from $relationshipEntityAwareDto.entity
      $primaryClient: Client($primaryBirthDate: birthDate) from $relationship.primaryClient
      $secondaryClient: Client($secondaryBirthDate: birthDate) from $relationship.secondaryClient
      $clientRelationship : ClientRelationship(
                startDate != null,
                ($secondaryBirthDate != null && startDate.isBefore($secondaryBirthDate)) ||
                ($primaryBirthDate != null && startDate.isBefore($primaryBirthDate))) from $relationshipEntityAwareDto.entity
  then
      IssueDetails details = new IssueDetails();
      details.setCode("R-00756");
        details.setUserMessage(
            String.format(
              "The start date cannot be less than the birth date."
                  + " dateStart: '%s';"
                  + " primary client birthDate: %tm/%<td/%<tY;"
                  + " secondary client birthDate: %tm/%<td/%<tY;",
              $clientRelationship.getStartDate(),
              $primaryBirthDate,
              $secondaryBirthDate
            )
          );
      details.setTechnicalMessage("The start date cannot be less than the birth date");
      validationDetailsList.add(details);
end
