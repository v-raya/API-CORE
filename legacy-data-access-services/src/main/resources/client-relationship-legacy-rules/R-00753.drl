import gov.ca.cwds.rest.exception.IssueDetails
import gov.ca.cwds.data.legacy.cms.entity.Client
import gov.ca.cwds.data.legacy.cms.entity.ClientRelationship
import gov.ca.cwds.cms.data.access.dto.ClientRelationshipAwareDTO;

global java.util.Set<IssueDetails> validationDetailsList

rule "R-00753"
  dialect "mvel"
  agenda-group "client-relationship-agenda"
  when
      /*
        Code: R - 00753

        Rule Txt:
         For all CLIENT RELATIONSHIP, if two or more have the same primary CLIENT and secondary CLIENT - the date ranges cannot be overlapping

        Logic:

      */
      $relationshipEntityAwareDto: ClientRelationshipAwareDTO()
      $clientRelationship : ClientRelationship($endDate1 : endDate, $startDate1 : startDate) from $relationshipEntityAwareDto.clientRelationshipList
      $relationshipForUpdate : ClientRelationship(
         (($clientRelationship.rightSide.identifier == rightSide.identifier && $clientRelationship.leftSide.identifier == leftSide.identifier)
       &&  (startDate == null || endDate == null || startDate <= $endDate1)
       && ($startDate1 == null || endDate == null || $startDate1 <= endDate)
       && ($startDate1 == null || endDate == null || $startDate1 <= endDate)
       && ($startDate1 == null || $endDate1 == null || $startDate1 <= $endDate1))
      ) from $relationshipEntityAwareDto.entity
      then
      IssueDetails details = new IssueDetails();
      details.setCode("R-00753");
        details.setUserMessage(
            String.format(
              "Overlap ERROR relationship."
                  + " dateStart: '%s';"
                  + " dateEnd: %tm/%<td/%<tY;"
                  + " overlapStart: %tm/%<td/%<tY;"
                  + " overlapEnd: %tm/%<td/%<tY",
              $startDate1,
              $endDate1,
              $relationshipForUpdate.getStartDate(),
              $relationshipForUpdate.getEndDate()
            )
          );
      details.setTechnicalMessage("For all CLIENT RELATIONSHIP, if two or more have the same primary CLIENT and secondary CLIENT - the date ranges cannot be overlapping");
      validationDetailsList.add(details);
end
