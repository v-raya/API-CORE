import gov.ca.cwds.rest.exception.IssueDetails
import gov.ca.cwds.data.legacy.cms.entity.Client
import gov.ca.cwds.cms.data.access.dto.ClientEntityAwareDTO;

global java.util.Set<IssueDetails> validationDetailsList

rule "R-00739"
  dialect "mvel"
  agenda-group "client-agenda"
  when
      /*
        Code: R - 00739

        Rule Txt:
        The client's birth date must be less than or equal to the death date of the client.
        If the client's death date is entered, then the client's birth date cannot be changed
        to a date later than that of the client's death date.

        Logic:
        CLIENT.Death_Date >= CLIENT.Birth_Date.
      */
      $clientAwareDTO: ClientEntityAwareDTO()
      $client: Client(birthDate != null, deathDate != null, birthDate.isAfter(deathDate)) from $clientAwareDTO.entity
  then
      IssueDetails details = new IssueDetails();
      details.setCode("R-00739");
      details.setUserMessage(String.format(
          "The client's birth date must be less than or equal to the death date of the client. "
          + "Client ID:'%s', Client Birth Date: %tm/%td/%tY, Client Death Date: %tm/%td/%tY",
          $client.getIdentifier(), $client.getBirthDate(), $client.getDeathDate()));
      details.setTechnicalMessage("If Client.birthDate != null and Client.deathDate != null then should be: CLIENT.deathDate >= CLIENT.birthDate");
      validationDetailsList.add(details);
end
