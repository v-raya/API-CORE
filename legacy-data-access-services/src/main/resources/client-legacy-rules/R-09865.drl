import gov.ca.cwds.rest.exception.IssueDetails;
import gov.ca.cwds.data.legacy.cms.entity.ChildClient;
import gov.ca.cwds.data.legacy.cms.entity.SpecialEducation;
import gov.ca.cwds.cms.data.access.dto.ChildClientEntityAwareDTO;

global java.util.Set validationDetailsList

rule "R-09865"
  dialect "mvel"
  agenda-group "client-agenda"
  when
      /*
        Code: R - 09865

        Rule Txt:
        The Special Education Start Date cannot be less than the Birth Date (if it exists)
        of the Client. If the Special Education Start Date is entered, then the client's birth
        date cannot be changed to a date later than the Special Education Start Date.

        Logic:
        SPECIAL_EDUCATION_INFORMATION.Start_Date must be >= CLIENT.Birth_Date.
      */
      $clientAwareDTO: ChildClientEntityAwareDTO()
      $specialEducation: SpecialEducation($startDate: startDate) from $clientAwareDTO.getSpecialEducations()
      $client: ChildClient(birthDate != null, $startDate == null || birthDate.isAfter($startDate)) from $clientAwareDTO.entity
  then
      IssueDetails details = new IssueDetails();
      details.setCode("R-09865");
      details.setUserMessage("The Special Education Start Date cannot be less than the Birth Date (if it exists)\n"
                              + " of the Client. If the Special Education Start Date is entered, then the client's birth\n"
                              + "  date cannot be changed to a date later than the Special Education Start Date.");
      details.setTechnicalMessage("If Client.birthDate != null then should be: SpecialEducation.startDate >= Client.birthDate");
      validationDetailsList.add(details);
end