import gov.ca.cwds.rest.exception.IssueDetails;
import gov.ca.cwds.data.legacy.cms.entity.Client;
import gov.ca.cwds.data.legacy.cms.entity.ParentalRightsTermination;
import gov.ca.cwds.cms.data.access.dto.ChildClientEntityAwareDTO;

global java.util.Set validationDetailsList

rule "R-00769"
  dialect "mvel"
  agenda-group "client-agenda"
  when
      /*
        Code: R - 00769

        Rule Txt:
        The Parental Rights Termination Date cannot be less than the Birth Date (if it exists) of \
        the Child Client. If the Parental Rights Termination Date is entered, then the client's
        birth date cannot be changed to a date later than that of the Parental Rights Termination Date.

        Logic:
        Select CLIENT > CHILD_CLIENT > PARENTAL_RIGHT_TERMINATION.Date >= CLIENT.Birth_Date.
      */
      $clientAwareDTO: ChildClientEntityAwareDTO()
      Client($birthDate: birthDate != null) from $clientAwareDTO.entity
      ParentalRightsTermination(date.isBefore($birthDate)) from $clientAwareDTO.parentalRightsTerminations
  then
      IssueDetails details = new IssueDetails();
      details.setCode("R-00769");
      details.setUserMessage("The Parental Rights Termination Date cannot be less than the birth date of the Child Client.");
      details.setTechnicalMessage("If Client.birthDate != null then for each ParentalRightsTermination should be: ParentalRightsTermination.date >= Client.birthDate");
      validationDetailsList.add(details);
end