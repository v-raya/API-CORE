import gov.ca.cwds.rest.exception.IssueDetails;
import gov.ca.cwds.data.legacy.cms.entity.Client;
import gov.ca.cwds.data.legacy.cms.entity.ClientRelationship;
import gov.ca.cwds.data.legacy.cms.entity.ParentalRightsTermination;
import gov.ca.cwds.cms.data.access.dto.ClientEntityAwareDTO

global java.util.Set validationDetailsList

rule "R-00756"
  dialect "mvel"
  agenda-group "client-agenda"
  when
      /*
        Code: R - 00756

        Rule Txt:
        The start date cannot be less than the birth date (if it exists) of either the Primary or
        Secondary Client. This rule only applies when both the primary and secondary Clients have birth dates entered.

        Logic:
        CLIENT_RELATIONSHIP.Start_Date CLIENT_RELATIONSHIP > (primary) CLIENT.Birth_Date
        and CLIENT_RELATIONSHIP.Start_Date CLIENT_RELATIONSHIP > (secondary) CLIENT.Birth_Date.
      */
      $clientAwareDTO: ClientEntityAwareDTO()
      $client: Client($birthDate: birthDate != null) from $clientAwareDTO.entity
      $relationship: ClientRelationship(startDate != null, startDate.isBefore($birthDate)) from $clientAwareDTO.clientRelationships
  then
      IssueDetails details = new IssueDetails();
      details.setCode("R-00756");
      details.setUserMessage(String.format("The Client Relationship Start Date cannot be less than the birth date of the Client."
      + " Client ID:'%s' , Client birth date: %tm/%<td/%<tY, ClientRelationship ID:'%s', ClientRelationship Start Date: %tm/%<td/%<tY",
      $client.getIdentifier(), $client.getBirthDate(), $relationship.getIdentifier(), $relationship.getStartDate()));
      details.setTechnicalMessage("If Client.birthDate != null then for each Client's ClientRelationship should be: "
      + "if ClientRelationship.startDate != null then ClientRelationship.startDate >= Client.birthDate");
      validationDetailsList.add(details);
end