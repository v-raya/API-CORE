import gov.ca.cwds.rest.exception.IssueDetails
import gov.ca.cwds.data.legacy.cms.entity.PlacementHome
import gov.ca.cwds.data.legacy.cms.entity.OtherAdultsInPlacementHome
import gov.ca.cwds.cms.data.access.dto.PlacementHomeEntityAwareDTO
import gov.ca.cwds.cms.data.access.dto.OtherAdultInHomeEntityAwareDTO
import gov.ca.cwds.security.realm.PerryAccount

global java.util.Set validationDetailsList

rule "R-00179"
  dialect "mvel"
  agenda-group "placement-home-agenda"
  when
      /*
      Code: R - 00179

      Field: fraOtherAdults.InHome

      Rule Txt
      The BIRTH DATE must be <= (system date - 18 years).

      Logic
      OTHER_ADULTS_IN_PLACEMENT_HOME: Birth_Date must be <= (system date - 18 years)
      */
      $placementHomeAwareDTO: PlacementHomeEntityAwareDTO()

      PlacementHomeEntityAwareDTO($list : otherAdultInHomeParameterObjects)
      $otherAdultDTO: OtherAdultInHomeEntityAwareDTO() from $list
      $otherAdults : OtherAdultsInPlacementHome(birthDt != null, eval(birthDt.isAfter(LocalDate.now().minusYears(18)))) from $otherAdultDTO.entity
  then
      IssueDetails details = new IssueDetails();
      details.setCode("R-00179");
      details.setProperty("OtherAdultsInPlacementHome.birthDt");
      details.setInvalidValue($otherAdults.getBirthDt());
      details.setUserMessage("Other adults' birth date must be more then system date - 18 years. Other adults ID: " + $otherAdults.identifier);
      details.setTechnicalMessage("Violation of rule: OTHER_ADULTS_IN_PLACEMENT_HOME: Birth_Date must be <= (system date - 18 years)");
      validationDetailsList.add(details);
end


